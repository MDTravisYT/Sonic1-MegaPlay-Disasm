PALETTEFADEIN:
		MOVE.W	#$3F,	(V_PFADE_START).W
		
	PALFADEIN_ALT:
		MOVEQ	#0,	D0
		LEA		(V_PAL_DRY).W,	A0
		MOVE.B	(V_PFADE_START).W,	D0
		ADDA.W	D0,	A0
		MOVEQ	#0,	D1
		MOVE.B	(V_PFADE_START+1).W,	D0
		
	PALFADEIN_FILL:
		MOVE.W	D1,	(A0)+
		DBF		D0,	PALFADEIN_FILL
		MOVE.W	#$15,	D4
		
	PALFADEIN_LOOP:
		MOVE.B	#$12,	(V_VBLA_ROUTINE).W
		BSR.W	WAITFORVBLA
		BSR.S	FADEIN_FROMBLACK
		BSR.W	RUNPLC
		DBF		D4,	PALFADEIN_LOOP
		RTS
		
FADEIN_FROMBLACK:
		MOVEQ	#0,	D0
		LEA		(V_PAL_DRY).W,	A0
		LEA		(V_PAL_DRY_DUP).W,	A1
		MOVE.B	(V_PFADE_START).W,	D0
		ADDA.W	D0,	A0
		ADDA.W	D0,	A1
		MOVE.B	(V_PFADE_START+1).W,	D0
		
	FIFB_ADDCOLOR:
		BSR.S	FADEIN_ADDCOLOR
		DBF		D0,	FIFB_ADDCOLOR
		CMPI.B	#1,	(V_ZONE).W
		BNE.S	FIFB_EXIT
		MOVEQ	#0,	D0
		LEA		(V_PAL_WATER).W,	A0
		LEA		(V_PAL_WATER_DUP).W,	A1
		MOVE.B	(V_PFADE_START).W,	D0
		ADDA.W	D0,	A0
		ADDA.W	D0,	A1
		MOVE.B	(V_PFADE_START+1).W,	D0
		
	FIFB_ADDCOLOR2:
		BSR.S	FADEIN_ADDCOLOR
		DBF		D0,	FIFB_ADDCOLOR2
		
	FIFB_EXIT:
		RTS
		
FADEIN_ADDCOLOR:
	FIAC_BLUE:
		MOVE.W	(A1)+,	D2
		MOVE.W	(A0),	D3
		CMP.W	D2,	D3
		BEQ.S	FIAC_NEXT
		MOVE.W	D3,	D1
		ADDI.W	#$200,	D1
		CMP.W	D2,	D1
		BHI.S	FIAC_GREEN
		MOVE.W	D1,	(A0)+
		RTS
		
	FIAC_GREEN:
		MOVE.W	D3,	D1
		ADDI.W	#$20,	D1
		CMP.W	D2,	D1
		BHI.S	FIAC_RED
		MOVE.W	D1,	(A0)+
		RTS
		
	FIAC_RED:
		ADDQ.W	#2,	(A0)+
		RTS
		
	FIAC_NEXT:
		ADDQ.W	#2,	A0
		RTS