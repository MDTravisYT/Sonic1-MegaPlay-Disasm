NEMDEC:
		MOVEM.L	D0-D7/A0-A1/A3-A5,	-(SP)
		LEA		(NEMPCD_WRITEROWTOVDP).L,	A3
		LEA		($C00000).L,	A4
		BRA.S	NEMDECMAIN
		
NEMDECTORAM
		movem.l d0-d7/a0-a1/a3-a5,-(sp)
		LEA		(NEMPCD_WRITEROWTORAM).L,	A3
		
	NEMDECMAIN:
		LEA		(V_NGFX_BUFFER).W,	A1
		MOVE.W	(A0)+,	D2
		LSL.W	#1,	D2
		BCC.S	LOC_168A
		ADDA.W	#$A,	A3
		
	LOC_168A:
		LSL.W	#2,	D2
		MOVEA.W	D2,	A5
		MOVEQ	#8,	D3
		MOVEQ	#0,	D2
		MOVEQ	#0,	D4
		BSR.W	NEMDEC_BUILDCODETABLE
		MOVE.B	(A0)+,	D5
		ASL.W	#8,	D5
		MOVE.B	(A0)+,	D5
		MOVE.W	#$10,	D6
		BSR.S	NEMDEC_PROCESSCOMPRESSEDDATA
		MOVEM.L	(SP)+,	D0-D7/A0-A1/A3-A5
		RTS
		
NEMDEC_PROCESSCOMPRESSEDDATA:
		MOVE.W	D6,	D7
		SUBQ.W	#8,	D7
		MOVE.W	D5,	D1
		LSR.W	D7,	D1
		CMPI.B	#$FC,	D1
		BCC.S	NEMPCD_INLINEDATA
		ANDI.W	#$FF,	D1
		ADD.W	D1,	D1
		MOVE.B	(A1,D1.W),	D0
		EXT.W	D0
		SUB.W	D0,	D6
		CMPI.W	#9,	D6
		BCC.S	LOC_16D2
		ADDQ.W	#8,	D6
		ASL.W	#8,	D5
		MOVE.B	(A0)+,	D5
		
	LOC_16D2:
		MOVE.B	1(A1,D1.W),	D1
		MOVE.W	D1,	D0
		ANDI.W	#$F,	D1
		ANDI.W	#$F0,	D0
		
	NEMPCD_PROCESSCOMPRESSEDDATA:
		LSR.W	#4,	D0
		
	NEMPCD_WRITEPIXEL:
		LSL.L	#4,	D4
		OR.B	D1,	D4
		SUBQ.W	#1,	D3
		BNE.S	NEMPCD_WRITEPIXEL_LOOP
		JMP		(A3)
		
NEMPCD_NEWROW:
		MOVEQ	#0,	D4
		MOVEQ	#8,	D3
		
	NEMPCD_WRITEPIXEL_LOOP:
		DBF		D0,	NEMPCD_WRITEPIXEL
		BRA.S	NEMDEC_PROCESSCOMPRESSEDDATA
		
	NEMPCD_INLINEDATA:
		SUBQ.W	#6,	D6
		CMPI.W	#9,	D6
		BCC.S	LOC_1704
		ADDQ.W	#8,	D6
		ASL.W	#8,	D5
		MOVE.B	(A0)+,	D5
		
	LOC_1704:
		SUBQ.W	#7,	D6
		MOVE.W	D5,	D1
		LSR.W	D6,	D1
		MOVE.W	D1,	D0
		andi.w	#$F,	D1
		ANDI.W	#$70,	D0
		CMPI.W	#9,	D6
		BCC.S	NEMPCD_PROCESSCOMPRESSEDDATA
		ADDQ.W	#8,	D6
		ASL.W	#8,	D5
		MOVE.B	(A0)+,	D5
		BRA.S	NEMPCD_PROCESSCOMPRESSEDDATA
		
NEMPCD_WRITEROWTOVDP:
		MOVE.L	D4,	(A4)
		SUBQ.W	#1,	A5
		MOVE.W	A5,	D4
		BNE.S	NEMPCD_NEWROW
		RTS
		
NEMPCD_WRITEROWTOVDP_XOR:
		EOR.L	D4,	D2
		MOVE.L	D2,	(A4)
		SUBQ.W	#1,	A5
		MOVE.W	A5,	D4
		BNE.S	NEMPCD_NEWROW
		RTS
		
NEMPCD_WRITEROWTORAM:
		MOVE.L	D4,	(A4)+
		SUBQ.W	#1,	A5
		MOVE.W	A5,	D4
		BNE.S	NEMPCD_NEWROW
		RTS
		
NEMPCD_WRITEROWTORAM_XOR:
		EOR.L	D4,	D2
		MOVE.L	D2,	(A4)+
		SUBQ.W	#1,	A5
		MOVE.W	A5,	D4
		BNE.S	NEMPCD_NEWROW
		RTS
		
	NEMDEC_BUILDCODETABLE:
		MOVE.B	(A0)+,	D0
		
	NEMBCT_CHKEND:
		CMPI.B	#$FF,	D0
		BNE.S	NEMBCT_NEWPALINDEX
		RTS
		
	NEMBCT_NEWPALINDEX:
		MOVE.W	D0,	D7
		
	NEMBCT_LOOP:
		MOVE.B	(A0)+,	D0
		CMPI.B	#$80,	D0
		BCC.S	NEMBCT_CHKEND
		MOVE.B	D0,	D1
		ANDI.W	#$F,	D7
		ANDI.W	#$70,	D1
		OR.W	D1,	D7
		ANDI.W	#$F,	D0
		MOVE.B	D0,	D1
		LSL.W	#8,	D1
		OR.W	D1,	D7
		MOVEQ	#8,	D1
		SUB.W	D0,	D1
		BNE.S	NEMBCT_SHORTCODE
		MOVE.B	(A0)+,	D0
		ADD.W	D0,	D0
		MOVE.W	D7,	(A1,D0.W)
		BRA.S	NEMBCT_LOOP
		
	NEMBCT_SHORTCODE:
		MOVE.B	(A0)+,	D0
		LSL.W	D1,	D0
		ADD.W	D0,	D0
		MOVEQ	#1,	D5
		LSL.W	D1,	D5
		SUBQ.W	#1,	D5
		
	NEMBCT_SHORTCODE_LOOP:
		MOVE.W	D7,	(A1,D0.W)
		ADDQ.W	#2,	D0
		DBF	D5,	NEMBCT_SHORTCODE_LOOP
		BRA.S	NEMBCT_LOOP